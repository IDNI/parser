# populates TAU_PARSER_GIT_DESCRIBED, TAU_PARSER_GIT_BRANCH and TAU_PARSER_GIT_COMMIT_HASH variables
# and creates TAU_PARSER_GIT_DEFINITIONS list
# provides target_git_definitions($target) function to add definitions to a target

execute_process(
	COMMAND git describe --tags --always
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	OUTPUT_VARIABLE TAU_PARSER_GIT_DESCRIBED
	OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
	COMMAND git rev-parse --abbrev-ref HEAD
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	OUTPUT_VARIABLE TAU_PARSER_GIT_BRANCH
	OUTPUT_STRIP_TRAILING_WHITESPACE
)
execute_process(
	COMMAND git log -1 --format=%h
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	OUTPUT_VARIABLE TAU_PARSER_GIT_COMMIT_HASH
	OUTPUT_STRIP_TRAILING_WHITESPACE
)

set(TAU_PARSER_GIT_DEFINITIONS
	"TAU_PARSER_GIT_DESCRIBED=\"${TAU_PARSER_GIT_DESCRIBED}\""
	"TAU_PARSER_GIT_COMMIT_HASH=\"${TAU_PARSER_GIT_COMMIT_HASH}\""
	"TAU_PARSER_GIT_BRANCH=\"${TAU_PARSER_GIT_BRANCH}\""
)

function(target_git_definitions target access)
	target_compile_definitions(${target} ${access} ${TAU_PARSER_GIT_DEFINITIONS})
endfunction()